import java.util.Properties

plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "sonicpulse" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

repositories {
    maven { url = 'https://maven.lavalink.dev/releases' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://maven.terraformersmc.com/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "dev.arbjerg:lavaplayer:2.2.3"
    include "dev.arbjerg:lavaplayer:2.2.3"

    modImplementation "dev.lavalink.youtube:v2:1.17.0"
    include "dev.lavalink.youtube:v2:1.17.0"

    implementation 'org.lwjgl:lwjgl-openal:3.3.1'
    runtimeOnly 'org.lwjgl:lwjgl-openal:3.3.1:natives-windows'

    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'

    modCompileOnly "com.terraformersmc:modmenu:13.0.3"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mod_version", project.mod_version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset = "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "mod_version": project.mod_version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
    
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
    it.options.compilerArgs << '-Xlint:deprecation'
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
}

tasks.register("bumpVersion") {
    doLast {
        def props = new java.util.Properties()
        file('gradle.properties').withInputStream {
            props.load(it)
        }
        def version = props['mod_version']
        def (major, minor, patch) = version.tokenize('.')
        patch = (patch as int) + 1
      
        props['mod_version'] = "${major}.${minor}.${patch}"
        file('gradle.properties').withOutputStream {
            props.store(it, null)
        }
        println "Version bumped to ${props['mod_version']}"
    }
}

